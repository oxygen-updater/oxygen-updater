apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        signingConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        applicationId "com.arjanvlek.oxygenupdater"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 43
        versionName "2.6.2"
        signingConfig signingConfigs.signingConfig
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        return true
    }

    buildTypes {
        release { // Config for releases and testing on a real device. Uses the production server, and reads system properties using the OnePlus / OxygenOS specific build.prop values.
            buildConfigField "String", "SERVER_BASE_URL", "\"https://oxygenupdater.com/api/v2.3/\""
            buildConfigField "String", "FAQ_SERVER_URL", "\"https://oxygenupdater.com/inappfaq\""
            buildConfigField "String", "NOTIFICATIONS_PREFIX", "\"\""
            buildConfigField "String", "DEVICE_NAME_LOOKUP_KEY", "\"ro.display.series, ro.build.product\""
            buildConfigField "String", "OS_VERSION_NUMBER_LOOKUP_KEY", "\"ro.oxygen.version, ro.build.ota.versionname\""
            buildConfigField "String", "OS_OTA_VERSION_NUMBER_LOOKUP_KEY", "\"ro.build.version.ota\""
            buildConfigField "String", "BUILD_FINGERPRINT_LOOKUP_KEY", "\"ro.build.oemfingerprint, ro.build.fingerprint\"" // Latter one is only used on very old OOS versions.
            buildConfigField "String", "SUPPORTED_BUILD_FINGERPRINT_KEYS", "\"release-keys\"" // Only devices using a properly signed (a.k.a. official) version of OxygenOS are supported.
            buildConfigField "Boolean", "ADS_ARE_SUPPORTED", "true"

            minifyEnabled true
            shrinkResources false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug { // Config for use during debugging and testing on an emulator. Uses the test server, and reads system properties using the default build.prop values present on any Android device / emulator.
            buildConfigField "String", "SERVER_BASE_URL", "\"https://test.oxygenupdater.com/api/v2.3/\""
            buildConfigField "String", "FAQ_SERVER_URL", "\"https://test.oxygenupdater.com/inappfaq\""
            buildConfigField "String", "NOTIFICATIONS_PREFIX", "\"test_\""
            buildConfigField "String", "DEVICE_NAME_LOOKUP_KEY", "\"ro.product.name\""
            buildConfigField "String", "OS_VERSION_NUMBER_LOOKUP_KEY", "\"ro.build.version.release\""
            buildConfigField "String", "OS_OTA_VERSION_NUMBER_LOOKUP_KEY", "\"ro.build.version.incremental\""
            buildConfigField "String", "BUILD_FINGERPRINT_LOOKUP_KEY", "\"ro.build.fingerprint\""
            buildConfigField "String", "SUPPORTED_BUILD_FINGERPRINT_KEYS", "\"\"" // During debugging, all devices are supported.
            buildConfigField "Boolean", "ADS_ARE_SUPPORTED", "true" // Ads also need to be tested.

            debuggable true
            signingConfig signingConfigs.signingConfig
        }
        localDebug { // Config for use during debugging locally on an emulator. Uses localhost at port 8000, and reads system properties using the default build.prop values present on any Android device / emulator.
            buildConfigField "String", "SERVER_BASE_URL", "\"http://10.0.2.2:8000/api/v2.3/\""
            buildConfigField "String", "FAQ_SERVER_URL", "\"http://10.0.2.2:8000/inappfaq\""
            buildConfigField "String", "NOTIFICATIONS_PREFIX", "\"test_\""
            buildConfigField "String", "DEVICE_NAME_LOOKUP_KEY", "\"ro.product.name\""
            buildConfigField "String", "OS_VERSION_NUMBER_LOOKUP_KEY", "\"ro.build.version.release\""
            buildConfigField "String", "OS_OTA_VERSION_NUMBER_LOOKUP_KEY", "\"ro.build.version.incremental\""
            buildConfigField "String", "BUILD_FINGERPRINT_LOOKUP_KEY", "\"ro.build.fingerprint\""
            buildConfigField "String", "SUPPORTED_BUILD_FINGERPRINT_KEYS", "\"\"" // During debugging, all devices are supported.
            buildConfigField "Boolean", "ADS_ARE_SUPPORTED", "true" // Ads also need to be tested.

            debuggable true
            signingConfig signingConfigs.signingConfig
        }
        screenshot { // Config for use during screenshot taking. Uses the real server, disables ads and reads system properties using the default build.prop values present on any Android device / emulator.
            buildConfigField "String", "SERVER_BASE_URL", "\"https://oxygenupdater.com/api/v2.3/\""
            buildConfigField "String", "FAQ_SERVER_URL", "\"https://oxygenupdater.com/inappfaq\""
            buildConfigField "String", "NOTIFICATIONS_PREFIX", "\"\""
            buildConfigField "String", "DEVICE_NAME_LOOKUP_KEY", "\"ro.product.name\""
            buildConfigField "String", "OS_VERSION_NUMBER_LOOKUP_KEY", "\"ro.build.version.release\""
            buildConfigField "String", "OS_OTA_VERSION_NUMBER_LOOKUP_KEY", "\"ro.build.version.incremental\""
            buildConfigField "String", "BUILD_FINGERPRINT_LOOKUP_KEY", "\"ro.build.fingerprint\""
            buildConfigField "String", "SUPPORTED_BUILD_FINGERPRINT_KEYS", "\"\"" // During taking screenshots, all devices are supported.
            buildConfigField "Boolean", "ADS_ARE_SUPPORTED", "false" // On screenshots no ads!
            debuggable true
            signingConfig signingConfigs.signingConfig

        }
        device { // Config for use on test devices. Uses the test server, and reads system properties using the LineageOS values from build.prop.
            buildConfigField "String", "SERVER_BASE_URL", "\"https://test.oxygenupdater.com/api/v2.3/\""
            buildConfigField "String", "FAQ_SERVER_URL", "\"https://test.oxygenupdater.com/inappfaq\""
            buildConfigField "String", "NOTIFICATIONS_PREFIX", "\"test_\""
            buildConfigField "String", "DEVICE_NAME_LOOKUP_KEY", "\"ro.cm.device\""
            buildConfigField "String", "OS_VERSION_NUMBER_LOOKUP_KEY", "\"ro.cm.display.version\""
            buildConfigField "String", "OS_OTA_VERSION_NUMBER_LOOKUP_KEY", "\"ro.cm.display.version\""
            buildConfigField "String", "BUILD_FINGERPRINT_LOOKUP_KEY", "\"ro.build.fingerprint\""
            buildConfigField "String", "SUPPORTED_BUILD_FINGERPRINT_KEYS", "\"\"" // On test device all build types are supported.
            buildConfigField "Boolean", "ADS_ARE_SUPPORTED", "true" // On test device use ads!
            debuggable true
            signingConfig signingConfigs.signingConfig

        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
    }

    testBuildType "debug"
}

buildscript {
    repositories {
        mavenCentral()
    }
}

repositories {
    mavenCentral()
}



dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //General Android libraries
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:customtabs:28.0.0'

    //JSON libraries
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.3'

    //Date and time libraries
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'org.joda:joda-convert:1.9.2'

    //Firebase libraries
    implementation 'com.google.firebase:firebase-ads:15.0.1'
    implementation 'com.google.firebase:firebase-messaging:15.0.2'
    implementation 'com.google.android.gms:play-services-base:15.0.1'

    // Update downloader library
    implementation 'com.mindorks.android:prdownloader:0.5.0'


    // Update installation libraries
    implementation 'eu.chainfire:libsuperuser:1.0.0.201704021214'
    implementation 'andhradroid.dev:aFilechooser:1.0.1'

    // Java 8 backports:
    implementation 'net.sourceforge.streamsupport:streamsupport:1.6.0'

    // Test libraries
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    testImplementation 'com.squareup.okhttp3:mockwebserver:3.9.1'
}

apply plugin: 'com.google.gms.google-services'